define



pmid sub attribute,
    datatype long;

degree sub attribute,
    datatype string;  # TODO Add regex

identifier sub attribute,
    datatype string;


text sub attribute,
    datatype string;

name sub attribute,
    datatype string;

symbol sub attribute,
    datatype string;

definition sub attribute,
    datatype string;

score sub attribute,
    datatype double;

chemical sub entity,
    key identifier,
    plays from-actor,
    plays to-actor,
    plays superior-chemical,
    plays subordinate-chemical,
    plays associated-chemical,
    plays therapeutic,
    plays functional-chemical,
    plays inferred-from,
    has name,
    has definition;

gene sub entity,
    key identifier,
    plays from-actor,
    plays to-actor,
    plays inferred-from,
    plays associated-gene,
    has name,
    has symbol;

disease sub entity,
    key identifier,
    has name,
    has definition,
    plays superior-disease,
    plays subordinate-disease,
    plays treated-disease,
    plays associated-disease,
    plays treated-disease,
    plays disease-function-of,
    plays associated-disease;

hierarchy sub relation,
    relates superior,
    relates subordinate;

disease-hierarchy sub hierarchy,
    relates superior-disease as superior,
    relates subordinate-disease as subordinate;

chemical-hierarchy sub hierarchy,
    relates superior-chemical as superior,
    relates subordinate-chemical as subordinate;

chemical-gene-interaction sub relation,
    relates from-actor,
    relates to-actor,
    plays sourced-data,
    plays from-actor,
    plays to-actor,
    has degree,
    key identifier,
    has text;

association sub relation,
    key identifier,
    relates associated,
    plays sourced-data,
    plays inferred-conclusion;

chemical-disease-association sub association,
    relates associated-chemical as associated,
    relates associated-disease as associated;

therapy sub chemical-disease-association,
    relates therapeutic as associated-chemical,
    relates treated-disease as associated-disease;

functional-association sub chemical-disease-association,
    relates functional-chemical as associated-chemical,
    relates disease-function-of as associated-disease;

gene-disease-association sub association,
    relates associated-disease as associated,
    relates associated-gene as associated;

inference sub relation,
    relates inferred-conclusion,
    relates inferred-from,
    has score;

data-sourcing sub relation,
    relates sourced-data,
    relates data-source;

pubmed-citation sub entity,
    key pmid,
    plays data-source;

# Rules
disease-disease-association sub relation,
    relates associated-disease;

disease-with-common-parent-are-related sub rule,
when {
    $d1 isa disease;
    $d2 isa disease;
    $d1 != $d2;
    $d3 isa disease;
    (superior-disease: $d3, subordinate-disease: $d1) isa disease-hierarchy;
    (superior-disease: $d3, subordinate-disease: $d2) isa disease-hierarchy;
}, then {
    (associated-disease: $d1, associated-disease: $d2) isa disease-disease-association;
};

parent-disease-is-related sub rule,
when {
    $d1 isa disease;
    $d2 isa disease;
    (superior-disease: $d1, subordinate-disease: $d2) isa disease-hierarchy;
}, then {
    (associated-disease: $d1, associated-disease: $d2) isa disease-disease-association;
};

disease-is-related-to-itself sub rule,
when {
    $d1 isa disease;
}, then {
    (associated-disease: $d1) isa disease-disease-association;
};