define

example-id sub attribute,
    value long;

age sub attribute,
    value long;

name sub attribute,
    value string;

severity sub attribute,
    value double;

units-per-week sub attribute,
    value long;

person sub entity,
    key example-id,
    has age,
    plays patient,
    plays candidate-patient,
    plays symptomatic-patient,
    plays consumer,
    plays person-at-risk,
    plays parent,
    plays child,
    plays doctor;

disease sub entity,
    key name,
    plays cause,
    plays diagnosed-disease,
    plays candidate-diagnosed-disease,
    plays risked-disease;

substance sub entity,
    key name,
    plays consumed-substance;

parentship sub relation,
    relates child,
    relates parent;

diagnosis sub relation,
    relates patient,
    relates diagnosed-disease,
    relates doctor,
    relates diagnoser,
    has probability-exists,
    has probability-non-exists,
    has probability-preexists;

candidate-diagnosis sub relation,
    relates candidate-patient,
    relates candidate-diagnosed-disease;

probability-exists sub attribute,
    value double;

probability-non-exists sub attribute,
    value double;

probability-preexists sub attribute,
    value double;

kgcn sub entity,
	plays diagnoser;

where-no-diagnosis-add-candidate-diagnosis sub rule,
    when {
        $p isa person;
        $d isa disease;
        not{ (patient: $p, diagnosed-disease: $d) isa diagnosis; };
    }, then {
        (candidate-patient: $p, candidate-diagnosed-disease: $d) isa candidate-diagnosis;
    };

causality sub relation,
    relates cause,
    relates effect;

symptom sub entity,
    key name,
    plays presented-symptom,
    plays effect;

symptom-presentation sub relation,
    has severity,
    relates presented-symptom,
    relates symptomatic-patient;

risk-factor sub relation,
    relates person-at-risk,
    relates risked-disease;

consumption sub relation,
    has units-per-week,
    relates consumer,
    relates consumed-substance;

alcohol-risk-of-diabetes sub rule,
when {
    $p isa person;
    $c(consumer: $p, consumed-substance: $s) isa consumption, has units-per-week $u;
    $u >= 10;
    $s isa substance, has name "Alcohol";
    $d isa disease, has name "Diabetes Type II";
}, then {
	(person-at-risk: $p, risked-disease: $d) isa alcohol-risk-factor;
};

over-40-risk-of-diabetes sub rule,
when {
    $p isa person, has age $a;
    $a >= 40;
    $d isa disease, has name "Diabetes Type II";
}, then {
	(person-at-risk: $p, risked-disease: $d) isa age-risk-factor;
};

heriditary-risk-of-diabetes sub rule,
when {
    $p isa person;
    $parent isa person;
    $cause(parent: $parent, child: $p) isa parentship;
    $dia(patient: $parent, diagnosed-disease: $d) isa diagnosis;
    $d isa disease, has name "Diabetes Type II";
}, then {
	(person-at-risk: $p, risked-disease: $d) isa hereditary-risk-factor;
};


smoking-risk-of-multiple-sclerosis sub rule,
when {
    $p isa person;
    $c(consumer: $p, consumed-substance: $s) isa consumption, has units-per-week $u;
    $u >= 12;
    $s isa substance, has name "Cigarettes";
    $d isa disease, has name "Multiple Sclerosis";
}, then {
	(person-at-risk: $p, risked-disease: $d) isa smoking-risk-factor;
};

hereditary-risk-factor sub risk-factor,
    relates person-at-risk,
    relates risked-disease;

age-risk-factor sub risk-factor,
    relates person-at-risk,
    relates risked-disease;

alcohol-risk-factor sub risk-factor,
    relates person-at-risk,
    relates risked-disease;

smoking-risk-factor sub risk-factor,
    relates person-at-risk,
    relates risked-disease;

# Add elements to the Knowledge Graph that are common to all examples
insert
    $ms isa disease, has name "Multiple Sclerosis";
    $diabetes isa disease, has name "Diabetes Type II";
    $fatigue isa symptom, has name "Fatigue";
    $vision isa symptom, has name "Blurred vision";
    $c(cause: $ms, effect: $fatigue) isa causality;
    $c1(cause: $ms, effect: $vision) isa causality;
    $c2(cause: $diabetes, effect: $fatigue) isa causality;
    $c3(cause: $diabetes, effect: $vision) isa causality;

insert $s isa substance, has name "Alcohol";
insert $s isa substance, has name "Cigarettes";
insert $kgcn isa kgcn;